
apply plugin: 'android-library'
apply plugin: 'maven-publish'

project.version = rootProject.ext.androidVersionName

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    sourceSets.main.jni.srcDirs = []

    defaultConfig {
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
    }

    productFlavors {
        nettest {
        }
        mainnet {
        }
        develop {
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:21.+'
    mainnetCompile project(':core')
    nettestCompile project(':core')
    developCompile project(':core')
}

android.libraryVariants.all { variant -> 
  task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
    title = "Airbitz Core API"
    source = variant.javaCompile.source 
    classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath()) 

    options.links "http://docs.oracle.com/javase/7/docs/api/" 
    options.linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference" 
    options.stylesheetFile = new File(projectDir, "assets/stylesheet.css");
    options.addStringOption "top", new File(projectDir, "assets/top.html").text;
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.encoding = 'utf-8'
    options.docEncoding = 'utf-8'
    options.charSet = 'utf-8'
    exclude '**/BuildConfig.java' 
    exclude '**/R.java' 
    exclude '**/SWIG*' 
    exclude '**/internal/*.java' 
  }
  task("generate${variant.name.capitalize()}Userguide", type:Copy) {
    from new File(projectDir, "assets/userguide/userguide.html")
    into 'build/docs/'
  }
}

publishing {
    publications {
        mainnetAndroid(MavenPublication) {
            groupId 'co.airbitz'
            artifactId 'airbitz-core-android'
            version "$rootProject.ext.androidVersionName"
            artifact("$buildDir/outputs/aar/android-mainnet-release.aar")
        }
        testnetAndroid(MavenPublication) {
            groupId 'co.airbitz'
            artifactId 'airbitz-core-android-testnet'
            version "$rootProject.ext.androidVersionName"
            artifact("$buildDir/outputs/aar/android-nettest-release.aar")
        }
        developAndroid(MavenPublication) {
            groupId 'co.airbitz'
            artifactId 'airbitz-core-android-develop'
            version "$rootProject.ext.androidVersionName"
            artifact("$buildDir/outputs/aar/android-develop-release.aar")
        }
    }
}
