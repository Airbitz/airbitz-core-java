
apply plugin: 'android-library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    sourceSets.main.jni.srcDirs = []

    productFlavors {
        nettest {
        }
        mainnet {
        }
        develop {
        }
    }
}


dependencies {
    compile 'com.android.support:support-v4:21.+'
}

android.libraryVariants.all { variant -> 
  task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
    title = "Airbitz Core API"
    source = variant.javaCompile.source 
    classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath()) 

    options.links "http://docs.oracle.com/javase/7/docs/api/" 
    options.linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference" 
    options.stylesheetFile = new File(projectDir, "assets/stylesheet.css");
    options.addStringOption "top", new File(projectDir, "assets/top.html").text;
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.encoding = 'utf-8'
    options.docEncoding = 'utf-8'
    options.charSet = 'utf-8'
    exclude '**/BuildConfig.java' 
    exclude '**/R.java' 
    exclude '**/SWIG*' 
    exclude '**/internal/*.java' 
  }
  task("generate${variant.name.capitalize()}Userguide", type:Copy) {
	from new File(projectDir, "assets/userguide/userguide.html")
    into 'build/docs/'
  }
}

task buildAirbitzMainnet(type:Exec) {
    workingDir '..'
    commandLine './mkabc'
}

task copyAirbitzMainnet(type:Exec) {
    workingDir '..'
    commandLine './cpabc'
}

task buildAirbitzTestnet(type:Exec) {
    workingDir '..'
    environment 'ENABLE_TESTNET': 1
    commandLine './mkabc'
}

task copyAirbitzTestnet(type:Exec) {
    workingDir '..'
    environment 'ENABLE_TESTNET': 1
    commandLine './cpabc'
}

publishing {
    publications {
        mainnetLibrary(MavenPublication) {
            groupId 'co.airbitz'
            artifactId 'airbitz-core-android'
            version '0.9.2'
            artifact("$buildDir/outputs/aar/library-mainnet-release.aar")
        }
        testnetLibrary(MavenPublication) {
            groupId 'co.airbitz'
            artifactId 'airbitz-core-android-testnet'
            version '0.9.2'
            artifact("$buildDir/outputs/aar/library-nettest-release.aar")
        }
        developLibrary(MavenPublication) {
            groupId 'co.airbitz'
            artifactId 'airbitz-core-android-develop'
            version '0.9.2'
            artifact("$buildDir/outputs/aar/library-develop-release.aar")
        }
    }
}
